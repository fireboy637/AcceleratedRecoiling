plugins {
    id 'fabric-loom' version "${fabric_loom_version}"
}

base.setArchivesName("${mod_id}-fabric-${mod_version}+${minecraft_version}")
test.enabled = false

java {
    withSourcesJar()
    toolchain.languageVersion = JavaLanguageVersion.of(21)
}

dependencies {
    minecraft "com.mojang:minecraft:${minecraft_version}"
    mappings loom.layered() {
        officialMojangMappings()
        parchment("org.parchmentmc.data:parchment-${parchment_minecraft_version}:${parchment_mappings_version}@zip")
    }

    modImplementation "net.fabricmc:fabric-loader:${fabric_loader_version}"
    modImplementation "net.fabricmc.fabric-api:fabric-api:${fabric_api_version}"

    compileOnly project(':common')
}

tasks.withType(JavaCompile).configureEach {
    source(project(':common').sourceSets.main.allSource) // 将:common的源代码加入编译
}

jar.doLast {
    rootProject.removePreview archiveFile.get().asFile
}

test {
    enabled = false
}

processResources {
    filesMatching("fabric.mod.json") {
        expand project.properties
    }
    from project(':common').sourceSets.main.resources
}

loom {
    runs {
        named("client"){
            client()
            ideConfigGenerated(true)
            configName("Fabric Client")
            runDir("run")
            vmArgs('--enable-preview')
        }

        named("server"){
            server()
            ideConfigGenerated(true)
            configName("Fabric Server")
            runDir("run")
            vmArgs('--enable-preview')
        }
    }
}
