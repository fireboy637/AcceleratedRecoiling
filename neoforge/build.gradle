plugins {
    id 'net.neoforged.gradle.userdev' version '7.1.20'
}

base.setArchivesName("${mod_id}-neoforge-${mod_version}+${minecraft_version}")
test.enabled = false

java {
    withSourcesJar()
    toolchain.languageVersion = JavaLanguageVersion.of(21)
}

dependencies {
    implementation "net.neoforged:neoforge:${neoforge_version}"

    compileOnly project(':common')
}

tasks.withType(JavaCompile).configureEach {
    source(project(':common').sourceSets.main.allSource) // 将:common的源代码加入编译
}

jar.doLast {
    rootProject.removePreview archiveFile.get().asFile
}

processResources {
    filesMatching('META-INF/neoforge.mods.toml') {
        expand project.properties
    }
    from project(':common').sourceSets.main.resources
}

runs {
    configureEach {
        modSource project.sourceSets.main
    }

    client {
        systemProperty 'neoforge.enabledGameTestNamespaces', mod_id
        arguments '--enable-preview', '--enable-native-access=ALL-UNNAMED'
    }

    server {
        systemProperty 'neoforge.enabledGameTestNamespaces', mod_id
        arguments '--nogui','--enable-preview', '--enable-native-access=ALL-UNNAMED'
    }
}
